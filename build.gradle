apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "eclipse"

repositories {
  // Spock release versions are available from Maven Central
  mavenCentral()
  // only required if a snapshot version of Spock is used
  mavenRepo urls: "http://m2repo.spockframework.org/snapshots"
}

dependencies {
  // mandatory dependencies for using Spock
  groovy("org.codehaus.groovy:groovy-all:1.7.5")
  testCompile "org.spockframework:spock-core:0.5-groovy-1.7"

  // optional dependencies for using Spock
  testCompile "org.hamcrest:hamcrest-core:1.2" // only necessary if Hamcrest matchers are used
  testRuntime "cglib:cglib-nodep:2.2"     // enables mocking of classes (in addition to interfaces)
  testRuntime "org.objenesis:objenesis:1.2" // enables mocking of classes without default constructor (together with CGLIB)

  // dependencies used by examples in this project (not required for using Spock)
  testRuntime "com.h2database:h2:1.2.147"
}

sourceSets {
  main {
    groovy {
      srcDir 'src/groovy'
    }
  }
  
  test {
    groovy {
      srcDir 'test/groovy'
    }
  }
}

// copies all Jars used in this project into build/output/lib (not required for using Spock)
task collectJars(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.testRuntime
}

// creates bootstrap files (gradlew etc.) for Gradle build (not required for using Spock)
task wrapper(type: Wrapper) {
  gradleVersion = "0.9-rc-3"
  jarPath = "wrapper"
}




